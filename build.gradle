/*
 * Copyright © 2017-2019 WireGuard LLC.
 * Copyright © 2018-2019 Harsh Shandilya <msfjarvis@gmail.com>. All Rights Reserved.
 * SPDX-License-Identifier: Apache-2.0
 */
buildscript {
    repositories {
        google()
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath Libs.com_android_tools_build_gradle
        classpath Libs.kotlin_gradle_plugin
    }
}

plugins {
    id 'de.fayard.buildSrcVersions' version '0.7.0'
    id 'com.diffplug.gradle.spotless' version '3.25.0'
}

buildSrcVersions {
}

subprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
    }
    if (plugins.hasPlugin('kotlin-android')) {
        dependencies {
            implementation Libs.kotlin_stdlib_jdk8
        }
    }
    if (it.name == 'app') {
        apply plugin: 'com.android.application'
    } else {
        apply plugin: 'com.android.library'
    }
    android {
        compileSdkVersion 29
        buildToolsVersion = '29.0.2'
        defaultConfig {
            minSdkVersion 21
            targetSdkVersion 29
            versionCode VersionConfiguration.versionCode
            versionName VersionConfiguration.versionName
        }
    }
}

project(':app') {
    apply from: '../spotless.gradle'
}

tasks {
    wrapper {
        distributionType = Wrapper.DistributionType.ALL
    }
}
